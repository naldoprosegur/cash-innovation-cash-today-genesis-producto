CREATE OR REPLACE PACKAGE SAPR_PREL_MAE_ACREDITACIONES IS

  PROCEDURE DATOS_CABECERA(P$OID_SUBCANAL IN VARCHAR2, P$OID_CERTIFICADO IN VARCHAR2, R$ OUT SYS_REFCURSOR);

  PROCEDURE DATOS_DETALLES_DIVISA(P$OID_SUBCANAL IN VARCHAR2, P$OID_CERTIFICADO IN VARCHAR2, P$COD_ISO_DIVISA IN VARCHAR2, R$ OUT SYS_REFCURSOR);

  PROCEDURE DATOS_DETALLES_RESUMEN(P$OID_SUBCANAL IN VARCHAR2, P$OID_CERTIFICADO IN VARCHAR2, R$ OUT SYS_REFCURSOR);

  PROCEDURE DATOS_DIVISAS(P$OID_SUBCANAL IN VARCHAR2, P$OID_CERTIFICADO IN VARCHAR2, R$ OUT SYS_REFCURSOR);

END SAPR_PREL_MAE_ACREDITACIONES;
/
CREATE OR REPLACE PACKAGE BODY SAPR_PREL_MAE_ACREDITACIONES AS

  PROCEDURE DATOS_CABECERA(P$OID_SUBCANAL IN VARCHAR2, P$OID_CERTIFICADO IN VARCHAR2, R$ OUT SYS_REFCURSOR)
  IS
  BEGIN

    OPEN R$ FOR
    WITH CERTIFICADOS_ANTERIORES AS (
      SELECT
        CESE.OID_CERTIFICADO,
        MAX(CERT.FYH_CERTIFICADO) FYH_CERTIFICADO
      FROM
        RPGE_YCERT_SALDO_EFECTIVO CESE
        INNER JOIN RPGE_YCERTIFICADO CERT ON CESE.OID_CERTIFICADO_ANTERIOR = CERT.OID_CERTIFICADO
      WHERE
        CESE.OID_CERTIFICADO = P$OID_CERTIFICADO
      GROUP BY
        CESE.OID_CERTIFICADO
    )
    SELECT
      CERT.OID_CERTIFICADO,
      CLIE.COD_CLIENTE,
      CLIE.DES_CLIENTE,
      CANA.COD_CANAL,
      CANA.DES_CANAL,
      SUCA.COD_SUBCANAL,
      SUCA.DES_SUBCANAL,
      DELE.COD_DELEGACION,
      DELE.DES_DELEGACION,
      FN_GMT_DELEGACION(DELE.OID_DELEGACION, CERT.FYH_CERTIFICADO)AS FYH_CERTIFICADO,
      CASE WHEN MAX(CEAN.FYH_CERTIFICADO) IS NOT NULL THEN FN_GMT_DELEGACION(DELE.OID_DELEGACION, MAX(CEAN.FYH_CERTIFICADO))
        ELSE NULL END FYH_CERTIFICADO_ANTERIOR
    FROM
      RPGE_YCERTIFICADO CERT
      INNER JOIN RPGE_YCONFIG_NIVEL_SALDO CONS ON CERT.OID_CONFIG_NIVEL_SALDO = CONS.OID_CONFIG_NIVEL_SALDO
      INNER JOIN RPGE_YCLIENTE CLIE ON CONS.OID_CLIENTE = CLIE.OID_CLIENTE
      INNER JOIN RPGE_YCERTIFICADOXSUBCANAL CESC ON CERT.OID_CERTIFICADO = CESC.OID_CERTIFICADO
      INNER JOIN RPGE_YSUBCANAL SUCA ON CESC.OID_SUBCANAL = SUCA.OID_SUBCANAL
      INNER JOIN RPGE_YCANAL CANA ON SUCA.OID_CANAL = CANA.OID_CANAL
      INNER JOIN RPGE_YCERTIFICADOXDELEGACION CEDE ON CERT.OID_CERTIFICADO = CEDE.OID_CERTIFICADO
      INNER JOIN RPGE_YDELEGACION DELE ON CEDE.OID_DELEGACION = DELE.OID_DELEGACION
      LEFT OUTER JOIN CERTIFICADOS_ANTERIORES CEAN ON CERT.OID_CERTIFICADO = CEAN.OID_CERTIFICADO
    WHERE
      CERT.OID_CERTIFICADO = P$OID_CERTIFICADO AND
      CESC.OID_SUBCANAL = P$OID_SUBCANAL
    GROUP BY
      CERT.OID_CERTIFICADO,
      CLIE.COD_CLIENTE,
      CLIE.DES_CLIENTE,
      CANA.COD_CANAL,
      CANA.DES_CANAL,
      SUCA.COD_SUBCANAL,
      SUCA.DES_SUBCANAL,
      DELE.OID_DELEGACION,
      DELE.COD_DELEGACION,
      DELE.DES_DELEGACION,
      CERT.FYH_CERTIFICADO;

  END DATOS_CABECERA;

  PROCEDURE DATOS_DETALLES_DIVISA(P$OID_SUBCANAL IN VARCHAR2, P$OID_CERTIFICADO IN VARCHAR2, P$COD_ISO_DIVISA IN VARCHAR2, R$ OUT SYS_REFCURSOR)
  IS
  BEGIN

    OPEN R$ FOR
    SELECT
      SECT.DES_SECTOR,
      TRUNC(DOCU.GMT_CREACION) FECHA,
      SUM(CASE WHEN TREF.BOL_DISPONIBLE = '1' THEN TREF.NUM_IMPORTE ELSE 0 END) IMPORTE_DISPONIBLE,
      SUM(CASE WHEN TREF.BOL_DISPONIBLE = '0' THEN TREF.NUM_IMPORTE ELSE 0 END) IMPORTE_NO_DISPONIBLES
    FROM
      RPGE_YDOCUMENTO DOCU
      INNER JOIN RPGE_YSECTOR SECT ON DOCU.OID_SECTOR_ORIGEN = SECT.OID_SECTOR
      INNER JOIN RPGE_YFORMULARIO FORM ON DOCU.OID_FORMULARIO = FORM.OID_FORMULARIO
      INNER JOIN RPGE_YTRANSACCION_EFECTIVO TREF ON DOCU.OID_DOCUMENTO = TREF.OID_DOCUMENTO
      INNER JOIN RPGE_YDIVISA DIVI ON TREF.OID_DIVISA = DIVI.OID_DIVISA
      INNER JOIN RPGE_YCUENTA CUEN ON TREF.OID_CUENTA_SALDO = CUEN.OID_CUENTA
    WHERE
      FORM.COD_FORMULARIO IN ('MAECIC', '194', '193', '207', 'MAECID') AND
      TREF.OID_CERTIFICADO = P$OID_CERTIFICADO AND
      CUEN.OID_SUBCANAL = P$OID_SUBCANAL AND
      DIVI.COD_ISO_DIVISA = P$COD_ISO_DIVISA
    GROUP BY
      SECT.DES_SECTOR,
      TRUNC(DOCU.GMT_CREACION)
    ORDER BY
      2;

  END DATOS_DETALLES_DIVISA;

  PROCEDURE DATOS_DETALLES_RESUMEN(P$OID_SUBCANAL IN VARCHAR2, P$OID_CERTIFICADO IN VARCHAR2, R$ OUT SYS_REFCURSOR)
  IS
  BEGIN

    OPEN R$ FOR
    SELECT
      DOCU.COD_EXTERNO,
      SECT.DES_SECTOR,
      TRUNC(DOCU.GMT_CREACION) FECHA,
      DIVI.DES_DIVISA,
      SUM(TREF.NUM_IMPORTE) NUM_IMPORTE
    FROM
      RPGE_YDOCUMENTO DOCU
      INNER JOIN RPGE_YSECTOR SECT ON DOCU.OID_SECTOR_ORIGEN = SECT.OID_SECTOR
      INNER JOIN RPGE_YFORMULARIO FORM ON DOCU.OID_FORMULARIO = FORM.OID_FORMULARIO
      INNER JOIN RPGE_YTRANSACCION_EFECTIVO TREF ON DOCU.OID_DOCUMENTO = TREF.OID_DOCUMENTO
      INNER JOIN RPGE_YDIVISA DIVI ON TREF.OID_DIVISA = DIVI.OID_DIVISA
      INNER JOIN RPGE_YCUENTA CUEN ON TREF.OID_CUENTA_SALDO = CUEN.OID_CUENTA
    WHERE
      FORM.COD_FORMULARIO IN ('MAECIC', '194', '193', '207', 'MAECID') AND
      TREF.OID_CERTIFICADO = P$OID_CERTIFICADO AND
      CUEN.OID_SUBCANAL = P$OID_SUBCANAL AND
      TREF.BOL_DISPONIBLE = '0'
    GROUP BY
      DOCU.COD_EXTERNO,
      SECT.DES_SECTOR,
      TRUNC(DOCU.GMT_CREACION),
      DIVI.DES_DIVISA
    ORDER BY
      3;

  END DATOS_DETALLES_RESUMEN;

  PROCEDURE DATOS_DIVISAS(P$OID_SUBCANAL IN VARCHAR2, P$OID_CERTIFICADO IN VARCHAR2, R$ OUT SYS_REFCURSOR)
  IS
  BEGIN

    OPEN R$ FOR
    WITH DIVISA_DEFAULT AS (
      SELECT
        P$OID_CERTIFICADO OID_CERTIFICADO,
        PAVA.DES_VALOR_PARAMETRO AS COD_ISO_DIVISA
      FROM
        RPGE_YPARAMETRO PARA
        INNER JOIN RPGE_YPARAMETRO_VALOR PAVA ON PARA.OID_PARAMETRO = PAVA.OID_PARAMETRO
        INNER JOIN RPGE_YNIVEL_PARAMETRO NIPA ON PARA.OID_NIVEL_PARAMETRO = NIPA.OID_NIVEL_PARAMETRO
      WHERE
        PARA.COD_PARAMETRO = 'CodigoIsoDivisaDefecto'
        AND NIPA.COD_NIVEL_PARAMETRO = 1 /*PAIS*/
        AND PAVA.COD_IDENTIFICADOR_NIVEL IN (SELECT DELE.COD_PAIS FROM RPGE_YVFILTROXCERTIFICADO FICE INNER JOIN RPGE_YDELEGACION DELE ON FICE.OID_DELEGACION = DELE.OID_DELEGACION WHERE FICE.COD_TIPO_FILTRO = 'delegacion' AND FICE.OID_CERTIFICADO = P$OID_CERTIFICADO)
        AND ROWNUM = 1
    )
    SELECT
      DIVI.COD_ISO_DIVISA,
      DIVI.DES_DIVISA,
      CASE WHEN DIVI.COD_ISO_DIVISA = DIDE.COD_ISO_DIVISA THEN 1 ELSE 2 END ORDEN,
      0 BOL_FALSA
    FROM
      RPGE_YDOCUMENTO DOCU
      INNER JOIN RPGE_YSECTOR SECT ON DOCU.OID_SECTOR_ORIGEN = SECT.OID_SECTOR
      INNER JOIN RPGE_YFORMULARIO FORM ON DOCU.OID_FORMULARIO = FORM.OID_FORMULARIO
      INNER JOIN RPGE_YTRANSACCION_EFECTIVO TREF ON DOCU.OID_DOCUMENTO = TREF.OID_DOCUMENTO
      INNER JOIN RPGE_YDIVISA DIVI ON TREF.OID_DIVISA = DIVI.OID_DIVISA
      INNER JOIN RPGE_YCUENTA CUEN ON TREF.OID_CUENTA_SALDO = CUEN.OID_CUENTA
      INNER JOIN DIVISA_DEFAULT DIDE ON TREF.OID_CERTIFICADO = DIDE.OID_CERTIFICADO
    WHERE
      FORM.COD_FORMULARIO IN ('MAECIC', '194', '193', '207', 'MAECID') AND
      TREF.OID_CERTIFICADO = P$OID_CERTIFICADO AND
      CUEN.OID_SUBCANAL = P$OID_SUBCANAL
    GROUP BY
      DIVI.COD_ISO_DIVISA,
      DIVI.DES_DIVISA,
      CASE WHEN DIVI.COD_ISO_DIVISA = DIDE.COD_ISO_DIVISA THEN 1 ELSE 2 END
    UNION
    SELECT
      'DETALLE_DEPOSITOS' COD_ISO_DIVISA,
      'Detalle de Depositos' DES_DIVISA,
      3 ORDEN,
      1 BOL_FALSA
    FROM
      DUAL
    ORDER BY
      3, 2;

  END DATOS_DIVISAS;

END SAPR_PREL_MAE_ACREDITACIONES;
/
